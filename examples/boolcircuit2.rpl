diagnosis := infer(check_circuit(FALSE, FALSE, TRUE, TRUE, FALSE));
print "Most plausible explanations:";
for (i := 0; i < len(diagnosis); i := i + 1) {
	print diagnosis[i];
};
 
define check_circuit(i1, i2, i3, o1, o2) {
	x1_broken := FALSE <<1>> TRUE;
	x2_broken := FALSE <<1>> TRUE;
	a1_broken := FALSE <<1>> TRUE;
	a2_broken := FALSE <<1>> TRUE;
	o1_broken := FALSE <<1>> TRUE;

	if (x1_broken) then l1 := FALSE <<0>> TRUE else l1 := (i1 ^ i2);
	if (a1_broken) then l2 := FALSE <<0>> TRUE else l2 := (i1 & i2);
	if (a2_broken) then l3 := FALSE <<0>> TRUE else l3 := (l1 & i3);
	if (x2_broken) then b2 := FALSE <<0>> TRUE else b2 := (l1 ^ i3);
	if (o1_broken) then b1 := FALSE <<0>> TRUE else b1 := (l3 | l2);

	observe b1 == o1 & b2 == o2;

	return "x1_broken: " + x1_broken + ", x2_broken " + x2_broken + 
		", a1_broken " + a1_broken +  ", a2_broken " + a2_broken +  ", o1_broken " + o1_broken;
};
